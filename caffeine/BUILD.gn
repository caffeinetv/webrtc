# Copyright (c) 2018 Caffeine Inc.
# All rights reserved

import("../webrtc.gni")

group("caffeine") {
  deps = [
    ":caffeine-rtc",
    ":x264enc",
  ]
}

rtc_static_library("x264enc") {
  sources = [
    "x264encoder.h",
    "x264encoder.cc",
  ]

  include_dirs = [
    "x264/include",
  ]

  # TODO: linux, ios, android
  if (is_win) {
    platform = "win"
  } else if (is_mac) {
    platform = "mac"
  }

  if (current_cpu == "x86") {
    bits = "32"
  } else {
    bits = "64"
  }

  lib_dirs = ["x264/lib/" + platform + bits]

  if (is_win) {
    libs = ["libx264.lib"]
  } else {
    libs = ["x264"]
  }

  if (!build_with_chromium && is_clang) {
    # Suppress warnings from the Chromium Clang plugin (bugs.webrtc.org/163).
    suppressed_configs += [ "//build/config/clang:find_bad_constructs" ]
  }

}

rtc_shared_library("caffeine-rtc") {
  sources = [
    "audiodevice.cc",
    "audiodevice.h",
    "audiodevicedefaultimpl.h",
    "caffeine.cc",
    "caffeine.h",
    "iceinfo.h",
    "interface.cc",
    "interface.h",
    "logsink.cc",
    "logsink.h",
    "peerconnectionobserver.cc",
    "peerconnectionobserver.h",
    "sessiondescriptionobserver.cc",
    "sessiondescriptionobserver.h",
    "stream.cc",
    "stream.h",
    "videocapturer.cc",
    "videocapturer.h",
  ]

  if (!build_with_chromium && is_clang) {
    # Suppress warnings from the Chromium Clang plugin (bugs.webrtc.org/163).
    suppressed_configs += [ "//build/config/clang:find_bad_constructs" ]
  }

  include_dirs = [
    "x264/include",
  ]

  deps = [
    ":x264enc",
    "../api/audio_codecs:builtin_audio_decoder_factory",
    "../api/audio_codecs:builtin_audio_encoder_factory",
    "../api/video_codecs:builtin_video_decoder_factory",
    "../api/video_codecs:builtin_video_encoder_factory",
    "../pc:libjingle_peerconnection",
    "../third_party/libyuv",
    "../third_party/jsoncpp",
    "../system_wrappers:runtime_enabled_features_default",
    "../system_wrappers:field_trial_default",
    "../system_wrappers:metrics_default",
    "../pc:rtc_pc",
  ]
}
